;; 编写程序确定使下列加密算法正确的字母值。每一个H、O、C、U、S、P、R、E和T唯一对应于0-9中得一个数字。且：
;;    HOCUS
;;+   POCUS
;;=  PRESTO

;;    ================
;;    H = 9
;;    O = 2
;;    C = 8
;;    U = 3
;;    S = 6
;;    P = 1
;;    R = 0
;;    E = 5
;;    T = 7
;;    ----------------
;;        HOCUS =>  92836
;;    +   POCUS =>  12836
;;    =  PRESTO => 105672

(deffacts 初始事实
	(H 0 1 2 3 4 5 6 7 8 9)
	(O 0 1 2 3 4 5 6 7 8 9)
	(C 0 1 2 3 4 5 6 7 8 9)
	(U 0 1 2 3 4 5 6 7 8 9)
	(S 0 1 2 3 4 5 6 7 8 9)
	(P 0 1 2 3 4 5 6 7 8 9)
	(R 0 1 2 3 4 5 6 7 8 9)
	(E 0 1 2 3 4 5 6 7 8 9)
	(T 0 1 2 3 4 5 6 7 8 9)
)

(defrule 唯一的规则
	(H $? ?H&~0 $?)
	(O $? ?O&~?H $?)
	(C $? ?C&~?H&~?O $?)
	(U $? ?U&~?H&~?O&~?C $?)
	(S $? ?S&~?H&~?O&~?C&~?U $?)
	(P $? ?P&~?H&~?O&~?C&~?U&~?S&~0 $?)
	(R $? ?R&~?H&~?O&~?C&~?U&~?S&~?P $?)
	(E $? ?E&~?H&~?O&~?C&~?U&~?S&~?P&~?R $?)
	(T $? ?T&~?H&~?O&~?C&~?U&~?S&~?P&~?R&~?E $?)
	(test (= (+ (+ (* 10000 ?H) (* 1000 ?O)(* 100 ?C)(* 10 ?U)(* 1 ?S))
	            (+ (* 10000 ?P) (* 1000 ?O)(* 100 ?C)(* 10 ?U)(* 1 ?S)))
	         (+ (* 100000 ?P)(* 10000 ?R) (* 1000 ?E)(* 100 ?S)(* 10 ?T)(* 1 ?O))))
=>
	(printout t 
	    "================" crlf
	    "H = " ?H crlf
	    "O = " ?O crlf
	    "C = " ?C crlf
	    "U = " ?U crlf
	    "S = " ?S crlf
	    "P = " ?P crlf
	    "R = " ?R crlf
	    "E = " ?E crlf
	    "T = " ?T crlf
	    "----------------" crlf
        "    HOCUS =>  " ?H ?O ?C ?U ?S crlf
        "+   POCUS =>  " ?P ?O ?C ?U ?S crlf
        "=  PRESTO => " ?P ?R ?E ?S ?T ?O crlf
	)
)

(reset)
(run)
(exit)
